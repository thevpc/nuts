options {
    STATIC = false ;
}

PARSER_BEGIN(TsonStreamParserImpl)
import net.thevpc.tson.impl.parser.*;
import net.thevpc.tson.*;

public class TsonStreamParserImpl implements TsonStreamParser{
    private TsonStreamParserConfig config;
    private TsonParserVisitor visitor;
    public void setConfig(TsonStreamParserConfig c){
        this.config=c;
        this.visitor=config.getVisitor();
    }

    private String prepareComments(String comments){
        if(!config.isRawComments()){
            return TsonParserUtils.parseComments(comments);
        }
        return comments;
    }
}}
}

PARSER_END(TsonStreamParserImpl)

public void parseDocument():
{
}
{
    parseElement() {
        visitor.visitDocumentEnd();
    }
}
public void parseElement():
{
}
{
  elementLevel2()
  |
  <EOF>

}

void elementLevel2() :
{
visitor.visitInstructionStart();
boolean simple=true;
}
{
    (
    elementLevel1()
    (<COLON> elementLevel1() {simple=false;visitor.visitKeyValueEnd();})?
    ){
        if(simple){
            visitor.visitSimpleEnd();
        }
    }

}



void elementLevel1() :
{
    net.thevpc.tson.impl.parser.javacc.Token comments=null;
    visitor.visitElementStart();
}
{
    (comments=<COMMENT> {
            if(!config.isSkipComments()) {
            visitor.visitComments(prepareComments(comments.image));
            }
})?
    (annotation())*
    (
    <NULL> { visitor.visitPrimitiveEnd(TsonNullImpl.INSTANCE);}
    | <TRUE>{ visitor.visitPrimitiveEnd(TsonBooleanImpl.TRUE);}
    |<FALSE>{  visitor.visitPrimitiveEnd(TsonBooleanImpl.FALSE);}
    |<DATETIME> {  visitor.visitPrimitiveEnd(TsonParserUtils.parseDateTimeElem(token.image));}
    |<DATE> {  visitor.visitPrimitiveEnd(TsonParserUtils.parseDateElem(token.image));}
    |<TIME> {  visitor.visitPrimitiveEnd(TsonParserUtils.parseTimeElem(token.image));}
    |<REGEX> {  visitor.visitPrimitiveEnd(TsonParserUtils.parseRegexElem(token.image));}
    |<BYTE>{ visitor.visitPrimitiveEnd(TsonParserUtils.parseByteElem(token.image));}
    |<SHORT>{  visitor.visitPrimitiveEnd(TsonParserUtils.parseShortElem(token.image));}
    |<LONG>{  visitor.visitPrimitiveEnd(TsonParserUtils.parseLongElem(token.image));}
    |<INTEGER>{ visitor.visitPrimitiveEnd(TsonParserUtils.parseIntElem(token.image));}

    |<BYTE_B>{ visitor.visitPrimitiveEnd(TsonParserUtils.parseByteElemBin(token.image));}
    |<BYTE_O>{ visitor.visitPrimitiveEnd(TsonParserUtils.parseByteElemOctal(token.image));}
    |<BYTE_H>{ visitor.visitPrimitiveEnd(TsonParserUtils.parseByteElemHex(token.image));}

    |<SHORT_B>{ visitor.visitPrimitiveEnd(TsonParserUtils.parseShortElemBin(token.image));}
    |<SHORT_O>{ visitor.visitPrimitiveEnd(TsonParserUtils.parseShortElemOctal(token.image));}
    |<SHORT_H>{ visitor.visitPrimitiveEnd(TsonParserUtils.parseShortElemHex(token.image));}

    |<INTEGER_B>{ visitor.visitPrimitiveEnd(TsonParserUtils.parseIntElemBin(token.image));}
    |<INTEGER_O>{ visitor.visitPrimitiveEnd(TsonParserUtils.parseIntElemOctal(token.image));}
    |<INTEGER_H>{ visitor.visitPrimitiveEnd(TsonParserUtils.parseIntElemHex(token.image));}


    |<LONG_B>{ visitor.visitPrimitiveEnd(TsonParserUtils.parseLongElemBin(token.image));}
    |<LONG_O>{ visitor.visitPrimitiveEnd(TsonParserUtils.parseLongElemOctal(token.image));}
    |<LONG_H>{ visitor.visitPrimitiveEnd(TsonParserUtils.parseLongElemHex(token.image));}

    |<FLOAT>{  visitor.visitPrimitiveEnd(TsonParserUtils.parseFloatElem(token.image));}
    |<DOUBLE>{ visitor.visitPrimitiveEnd(TsonParserUtils.parseDoubleElem(token.image));}
    |<CHARACTER>{  visitor.visitPrimitiveEnd(TsonParserUtils.parseCharElem(token.image));}
    |<STRING> {  visitor.visitPrimitiveEnd(TsonParserUtils.parseStringElem(token.image));}

//    |<NAN_DOUBLE> {  visitor.visitPrimitiveEnd(Tson.val(Double.NaN));}
//    |<NAN_FLOAT> {  visitor.visitPrimitiveEnd(Tson.val(Float.NaN));}
//    |<POS_INF_DOUBLE> {  visitor.visitPrimitiveEnd(Tson.val(Double.POSITIVE_INFINITY));}
//    |<NEG_INF_DOUBLE> {  visitor.visitPrimitiveEnd(Tson.val(Double.NEGATIVE_INFINITY));}
//    |<POS_INF_FLOAT> {  visitor.visitPrimitiveEnd(Tson.val(Float.POSITIVE_INFINITY));}
//    |<NEG_INF_FLOAT> {  visitor.visitPrimitiveEnd(Tson.val(Float.NEGATIVE_INFINITY));}
//    |<POS_BOUND_DOUBLE> {  visitor.visitPrimitiveEnd(Tson.val(Double.MAX_VALUE));}
//    |<NEG_BOUND_DOUBLE> {  visitor.visitPrimitiveEnd(Tson.val(Double.MIN_VALUE));}
//    |<POS_BOUND_FLOAT> {  visitor.visitPrimitiveEnd(Tson.val(Float.MAX_VALUE));}
//    |<NEG_BOUND_FLOAT> {  visitor.visitPrimitiveEnd(Tson.val(Float.MIN_VALUE));}
//    |<POS_BOUND_LONG> {  visitor.visitPrimitiveEnd(Tson.val(Long.MAX_VALUE));}
//    |<NEG_BOUND_LONG> {  visitor.visitPrimitiveEnd(Tson.val(Long.MIN_VALUE));}
//    |<POS_BOUND_INT> {  visitor.visitPrimitiveEnd(Tson.val(Integer.MAX_VALUE));}
//    |<NEG_BOUND_INT> {  visitor.visitPrimitiveEnd(Tson.val(Integer.MIN_VALUE));}
//    |<POS_BOUND_SHORT> {  visitor.visitPrimitiveEnd(Tson.val(Short.MAX_VALUE));}
//    |<NEG_BOUND_SHORT> {  visitor.visitPrimitiveEnd(Tson.val(Short.MIN_VALUE));}
//    | bin()
    | object(false)
    | array(false)
    | named()
    | unnamed()
   )
}

void annotation() :
{
  Token id=null;
}
{
    <AT> [id=<NAME>]
    {
        visitor.visitAnnotationStart(id==null?null:id.image);
    }
    <LPAREN>
    (<RPAREN>
         |
         (annotationParam() (<COMMA> annotationParam())*
         <RPAREN>)
    )
    {
         visitor.visitAnnotationEnd();
    }
}
//
//void bin() :
//{
//    InputStreamTsonBinaryStreamSource bin=new InputStreamTsonBinaryStreamSource();
//}
//{
//<BIN_START>
//(<BASE64>
//{
//    bin.pushBase64(token.image);
//}
//)*
//<END_BIN>
//{
//    visitor.visitPrimitiveEnd(new TsonBinaryStreamImpl(bin));
//}
//}


void named() :
{
  Token id;
    boolean hasPars=false;
    boolean hasArr=false;
    boolean hasObj=false;
}
{
    (id=<NAME>
    {
        visitor.visitNamedStart(id.image);
    })
    (
      (unnamed0(){hasPars=true;}) ?
    )
    (
        (array( true){hasArr=true;})
        |(object(true){hasObj=true;})
    )?
    {
        if(!hasArr && !hasObj){
            if(!hasPars){
                visitor.visitPrimitiveEnd(Tson.name(id.image));
            }else {
                visitor.visitFunctionEnd();
            }
        }
    }
}

void param() :
{
    visitor.visitParamElementStart();
}
{
    elementLevel2()
    {
        visitor.visitParamElementEnd();
    }
}

void unnamed() :
{
    boolean hasArr=false;
    boolean hasObj=false;
}
{
    unnamed0()
    (
         (array(true){hasArr=true;})
       | (object(true){hasObj=true;})
    )?
    {
    if(!hasArr && !hasObj){
            visitor.visitUpletEnd();
    }
}
}

void unnamed0() :
{
}
{
    (<LPAREN>
    {
    visitor.visitParamsStart(); }
    (
            <RPAREN>
            |
            (
            param()(<COMMA> param()) *
            <RPAREN>
            )
            )
    {visitor.visitParamsEnd(); }
    )
}

void objectElement() :
{
    visitor.visitObjectElementStart();
}
{
    elementLevel2()
    {
        visitor.visitObjectElementEnd();
    }
}

void arrayElement() :
{
    visitor.visitArrayElementStart();
}
{
    elementLevel2()
    {
        visitor.visitArrayElementEnd();
    }
}

void object(boolean named) :
{
}
{
    <LBRACE> {
        if(named){
            visitor.visitNamedObjectStart();
        }else{
            visitor.visitObjectStart();
        }
    }

    (
        <RBRACE>
        |
        ( objectElement()(<COMMA> objectElement()) *
        <RBRACE>
        )
        )
        {
            if(named){
                visitor.visitNamedObjectEnd();
            }else{
                visitor.visitObjectEnd();
            }
        }
}

void array(boolean named) :
{
}
{
    <LBRACK>
    {
        if(named){
           visitor.visitNamedArrayStart();
        }else{
            visitor.visitArrayStart();
        }
    }
    (
    <RBRACK>
    |
    ( arrayElement()(<COMMA> arrayElement()) *
    <RBRACK>
    )
    )
    {
         if(named){
              visitor.visitNamedArrayEnd();
         }else{
            visitor.visitArrayEnd();
         }
    }
}

void annotationParam() :
{
  visitor.visitAnnotationParamStart();
}
{
  elementLevel2()
  {
    visitor.visitAnnotationParamEnd();
  }
}


<DEFAULT> TOKEN :
{
    < DATETIME : (
                (<DIGIT>){4} "-" (<DIGIT>){2} "-" (<DIGIT>){2}
                ["T"](<DIGIT>){2} ":" (<DIGIT>){2} ":" (<DIGIT>){2} ("." (<DIGIT>){1,9})? ("Z" | ["+","-"](<DIGIT>){2} ":" (<DIGIT>){2})
               )
               >
|    < DATE : (
                ((<DIGIT>)){4} "-" ((<DIGIT>)){2} "-" (<DIGIT>){2}
               )
               >
|    < TIME : (
                ((<DIGIT>)){2} ":" ((<DIGIT>)){2} ":" ((<DIGIT>)){2} ("." ((<DIGIT>)){1,9})?
               )
               >
|    < REGEX : "/" ((["\\"] ~["\n"])| (~["*" , "/", "\\"]))*  "/" >
|     < LBRACE : "{" >
|    < RBRACE: "}" >
|    < SHORT: ("-")?<DIGIT> (<DIGIT>)* ("s"|"S") >
|    < BYTE: ("-")?<DIGIT> (<DIGIT>)* ("b"|"B") >
|    < LONG: ("-")?<DIGIT> (<DIGIT>)* ("L") >
|    < INTEGER: ("-")?<DIGIT> (<DIGIT>)*>
|    < INTEGER_H: ("-")?"0x" (["0"-"9","A","F","a","f"]){1,16}>
|    < INTEGER_O: ("-")?"0" (["0"-"7"]){1,16}>
|    < INTEGER_B: ("-")?"0b" (["0"-"1"]){1,32}>

|    < LONG_H: ("-")?"0x" (["0"-"9","A","F","a","f"]){1,16} "L">
|    < LONG_O: ("-")?"0" (["0"-"7"]){1,32}  "L">
|    < LONG_B: ("-")?"0b" (["0"-"1"]){1,64}  "L">

|    < SHORT_H: ("-")?"0x" (["0"-"9","A","F","a","f"]){1,16} ["s","S"]>
|    < SHORT_O: ("-")?"0" (["0"-"7"]){1,16}  ["s","S"]>
|    < SHORT_B: ("-")?"0b" (["0"-"1"]){1,16}  ["s","S"]>

|    < BYTE_H: ("-")?"0x" (["0"-"9","A","F","a","f"]){1,16} ["b","B"]>
|    < BYTE_O: ("-")?"0" (["0"-"7"]){1,16}  ["b","B"]>
|    < BYTE_B: ("-")?"0b" (["0"-"1"]){1,8}  ["b","B"]>

|    < FLOAT: ("-")?(<DIGIT>)+ "." (<DIGIT>)*
        (  (<EXPONENT>)?
        | "." (<DIGIT>)+ (<EXPONENT>)?
        | (<DIGIT>)+ <EXPONENT>) ("f"|"F")
     >
|    < DOUBLE: ("-")?(<DIGIT>)+ "." (<DIGIT>)* (<EXPONENT>)?
        | "." (<DIGIT>)+ (<EXPONENT>)?
        | (<DIGIT>)+ <EXPONENT>
     >
|    < #EXPONENT: ["e","E"] (["+","-"])? (<DIGIT>)+ >
|    < STRING: "\"" ( ~["\"","\\","\n","\r"] | "\\" ( ["n","t","b","r","f","\\","\'","\""] | ["0"-"7"] (["0"-"7"])?  | ["0"-"3"] ["0"-"7"] ["0"-"7"] | ( ["\n","\r"] | "\r\n")))* "\"">
|    < CHARACTER: "\'" (~["\'","\\","\n","\r"] | "\\" (["n","t","b","r","f","\\","\'","\""] | ["0"-"7"] (["0"-"7"])? | ["0"-"3"] ["0"-"7"] ["0"-"7"])) "\'">
|    < TRUE : "true" >
|    < FALSE : "false" >
|    < NULL : "null" >
//|    < DOT : "." >
|    < LPAREN : "(" >
|    < RPAREN : ")" >
|    < LBRACK : "[" >
|    < RBRACK : "]" >
|    < COLON : ":" >
//|    < SEMICOLON : ";" >
|    < COMMA : "," >
|    < AT : "@" >
//|    < BIN_START : "&[" > : IN_BIN

//|    < NAN_DOUBLE   : "NaN" | "NaN(double)" | "NaN(d)"  >
//|    < NAN_FLOAT : "NaN(float)" | "NaN(f)"  | "NaN(F)"  >
//|    < POS_INF_DOUBLE   : "+Inf" | "+Inf(double)" >
//|    < NEG_INF_DOUBLE   : "-Inf" | "-Inf(double)">
//|    < POS_INF_FLOAT   : "+Inf" | "+Inf(float)" | "+Inf(f)"  >
//|    < NEG_INF_FLOAT   : "-Inf" | "-Inf(float)" | "-Inf(f)"  >
//|    < POS_BOUND_DOUBLE : "+Bound(double)"| "+Bound(d)"  >
//|    < NEG_BOUND_DOUBLE : "-Bound(double)"| "-Bound(d)"  >
//|    < POS_BOUND_FLOAT : "+Bound(double)"| "+Bound(f)"  >
//|    < NEG_BOUND_FLOAT : "-Bound(double)"| "-Bound(f)"  >
//|    < POS_BOUND_LONG : "+Bound(long)"| "+Bound(L)"  >
//|    < NEG_BOUND_LONG : "-Bound(long)"| "-Bound(L)"  >
//|    < POS_BOUND_INT : "+Bound(int)"| "+Bound(i)"  >
//|    < NEG_BOUND_INT : "-Bound(int)"| "-Bound(i)"  >
//|    < POS_BOUND_SHORT : "+Bound(short)"| "+Bound(s)"  >
//|    < NEG_BOUND_SHORT : "-Bound(short)"| "-Bound(s)"  >

|    < COMMENT : "/*" (~["*"])* "*" ("*" | ~["*","/"] (~["*"])* "*")* "/" >
|    < NAME : ((<LETTER>) (<LETTER>| <DIGIT>) *) ("." ((<LETTER>) (<LETTER>| <DIGIT>) *))* >
|    < ALIAS : "&" ((<LETTER>) (<LETTER>| <DIGIT>) *) ("." ((<LETTER>) (<LETTER>| <DIGIT>) *))* >
|    < #LETTER:
        ["a"-"z","A"-"Z","_","$"]
    >
|    < #DIGIT:
        ["0"-"9"]
     >
}

//<IN_BIN> TOKEN:{
//      < END_BIN : "]" > : DEFAULT
//    | < BASE64 : (~["]"]) >
//}

SKIP : {
 " "
|  "\t"
|  "\n"
|  "\r"
}
